\schemaVariables {
	\skolemTerm int dummy;

	\modalOperator {diamond, box} #allmodal;
	\formula formula;

	\program ClangVariable #tvar, #tvar1, #tvar2;

	\program ClangValueType #vtype;
	\program ClangIntegerType #itype;
	\program ClangPointerType #ptype;
	\program ClangScalarType #stype;
	\program ClangObjectType #otype;

	\program ClangMember #member;

	\program ClangIntegerLiteral #ilit;

	\program ClangVariable #var, #var1, #var2, #var3;
	\program ClangValueVariable #vvar, #vvar1, #vvar2, #vvar3;
	\program ClangObjectVariable #ovar, #ovar1, #ovar2, #ovar3;
	\program ClangIntegerVariable #ivar, #ivar1, #ivar2, #ivar3;
	\program ClangPointerVariable #pvar, #pvar1, #pvar2, #pvar3;

	\program ClangValueSimpleExpression #vsexp, #vsexp1, #vsexp2, #vsexp3;
	\program ClangIntegerSimpleExpression #isexp, #isexp1, #isexp2, #isexp3;
	\program ClangPointerSimpleExpression #psexp, #psexp1, #psexp2, #psexp3;

	\program ClangExpression #exp, #exp1, #exp2;
	\program ClangObjectExpression #oexp, #oexp1, #oexp2;
	\program ClangValueExpression #vexp, #vexp1, #vexp2;
	\program ClangIntegerExpression #iexp, #iexp1, #iexp2;
	\program ClangPointerExpression #pexp, #pexp1, #pexp2;
	\program ClangComplexExpression #cexp, #cexp1, #cexp2;
	\program ClangValueComplexExpression #vcexp, #vcexp1, #vcexp2;
	\program ClangObjectComplexExpression #ocexp, #ocexp1, #ocexp2;

	\program ClangStatement #stmnt, #stmnt1, #stmnt2;

	\program ClangNonEmptyCompoundStatement #NonEmptyCompoundStatement;
	\program ClangBlockFrameVarDecl #BlockFrameVarDecl;
	\program ClangUnwindingBlockFrame #UnwindingBlockFrame;
	\program ClangEmptyBlockFrame #EmptyBlockFrame;
}


\rules {
	
	/* If statement */	

	/**
	 * Eliminating if statement.
	 */
	elimIf_1 {
		\find (\modality{#allmodal}.. if (#isexp) #stmnt ...\endmodality(formula))
		\sameUpdateLevel
		\varcond (\new(dummy, \dependingOn(formula)))
		"Normal Execution" : \replacewith ( 			
			\if (#ClangIntegerToInt(#isexp) != 0)
			\then (\modality{#allmodal}.. #stmnt ...\endmodality(formula))
			\else (\modality{#allmodal}.. ...\endmodality(formula))
			)
		\add(isValidVal(#isexp) ==>);
		"Valid Control Expression: #isexp" : \replacewith(undefinedBehavior(dummy)) \add(==> isValidVal(#isexp))
		\heuristics(simplify_prog)
	};	

	/**
	 * Eliminating if statement.
	 */
	elimIf_2 {
		\find (\modality{#allmodal}.. if (#isexp) #stmnt1 else #stmnt2 ...\endmodality(formula))
		\sameUpdateLevel
		\varcond (\new(dummy, \dependingOn(formula)))
		"Normal Execution" : \replacewith ( 			
			\if (#ClangIntegerToInt(#isexp) != 0)
			\then (\modality{#allmodal}.. #stmnt1 ...\endmodality(formula))
			\else (\modality{#allmodal}.. #stmnt2 ...\endmodality(formula))
			)
		\add(isValidVal(#isexp) ==>);
		"Valid Control Expression: #isexp" : \replacewith(undefinedBehavior(dummy)) \add(==> isValidVal(#isexp))
		\heuristics(simplify_prog)
	};
}