// This file is part of KeY - Integrated Deductive Software Design 
//
// Copyright (C) 2001-2011 Universitaet Karlsruhe (TH), Germany 
//                         Universitaet Koblenz-Landau, Germany
//                         Chalmers University of Technology, Sweden
// Copyright (C) 2011-2013 Karlsruhe Institute of Technology, Germany 
//                         Technical University Darmstadt, Germany
//                         Chalmers University of Technology, Sweden
//
// The KeY system is protected by the GNU General 
// Public License. See LICENSE.TXT for details.
// 

//


/**
  READ FIRST BEFORE EDITING:
    This file contains all rules moving an integer related assignment
  from the inside of a normalAssignment (diamond, box, etc.)
  modality to the logic (as an update). The Java operators are
  therefore mapped to their generic logic function. These generic
  functions (java...) are later interpreted as mathematical function
  dependent on the chosen Java semantics.

  In case of changes PLEASE preserve the files intended meaning. If
  you want to test some rules before implementing them completely,
  please create a new file and include it into the standardRules.key

  Files:
     // rules treating Java programs

     integerAssignment2UpdateRules.key: all integer related assignment
                                        rules for normal assignments      
     integerAssignment2UpdateRulesTout.key: all integer related assignment
                                        rules for throughout assignments      
     integerAssignment2UpdateRulesTraOnly.key: all integer related assignment
                                        rules for traonly assignments

     integerCommonRules.key: integer semantics independent rules which 
         transform programs

     // rules treating integer semantics sensitive functions 

     intRules.key: rules interpreting the generic translation
        functions 'java...' faithfully according to the Java Language
        Specification (sound, but a lot of divisions and modulo
        computation need to be performed)

     intRulesIgnoringOverflow.key: rules interpreting the generic translation
        functions 'java...' without overflow (not sound)  

     intRulesArith.key: rules interpreting the generic translation
        functions 'java...' as in intRulesIgnoringOverflow.key, but 
        requires to show absence of overflow or that the property to
        be proven does not depend on the value of an expression where
        an overflow occured (sound, but incomplete)
   
     // rules treating integer semantics insensitive functions 

     integerSimplificationRules.key: rules for arithmetic functions
         that do not depend on the chose integer semantics
        
*/

\schemaVariables {
  \modalOperator {diamond, box, diamond_transaction, box_transaction} #normalassign;

  \program SimpleExpression #se, #se0, #se1 ;
  \program JavaByteExpression #seByte, #seByte0, #seByte1 ;
  \program JavaShortExpression #seShort, #seShort0, #seShort1 ;
  \program JavaIntExpression #seInt, #seInt0, #seInt1 ;
  \program JavaLongExpression #seLong, #seLong0, #seLong1 ;
  \program JavaCharByteShortIntExpression #seCharByteShortInt, 
      #seCharByteShortInt0, #seCharByteShortInt1; 
  \program JavaByteShortExpression #seByteShort;

  \program Variable #loc ;

  \formula post;
}

\rules(programRules:Java) {

//------------------------------------------------------------------------
// ----------- Rules for binary arithmetic operators 
//------------------------------------------------------------------------

  //------------- multiplication

  assignmentMultiplicationInt  { 
      \find (\modality{#normalassign}{.. 
                 #loc = #seCharByteShortInt0 * #seCharByteShortInt1; 
             ...}\endmodality(post)) 
      \replacewith ({#loc:=javaMulInt(#seCharByteShortInt0,#seCharByteShortInt1)}
                        \modality{#normalassign}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "multiplication"
  };

  assignmentMultiplicationLong  { 
      \find (\modality{#normalassign}{.. 
                 #loc=#seCharByteShortInt * #seLong; 
             ...}\endmodality(post)) 
     \replacewith ({#loc:=javaMulLong(#seCharByteShortInt,#seLong)}
                      \modality{#normalassign}{.. ...}\endmodality(post)) 
     \heuristics(executeIntegerAssignment)
     \displayname "multiplication"
  };

  assignmentMultiplicationLong2  { 
      \find (\modality{#normalassign}{.. 
       	         #loc=#seLong * #seCharByteShortInt; 
             ...}\endmodality(post)) 
      \replacewith ({#loc:=javaMulLong(#seLong,#seCharByteShortInt)}
 		         \modality{#normalassign}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "multiplication"
  };

  assignmentMultiplicationLong3  { 
      \find(\modality{#normalassign}{.. 
                #loc=#seLong0 * #seLong1; 
            ...}\endmodality(post)) 
      \replacewith({#loc:=javaMulLong(#seLong0,#seLong1)}
                        \modality{#normalassign}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "multiplication"
  };
}

\rules(programRules:Java,runtimeExceptions:ban) {

  //------------- division

  assignmentDivisionInt  { 
      \find(==> \modality{#normalassign}{.. 
                #loc=#seCharByteShortInt0 / #seCharByteShortInt1; 
            ...}\endmodality(post)) 
      \replacewith (
           ==> {#loc:=javaDivInt(#seCharByteShortInt0, #seCharByteShortInt1)}
                            \modality{#normalassign}{.. ...}\endmodality(post));
      \replacewith (==> #seCharByteShortInt1 != 0)
      \heuristics(executeIntegerAssignment)
      \displayname "division"
  };

  assignmentDivisionLong  { 
      \find(==> \modality{#normalassign}{.. 
                #loc=#se / #seLong; 
            ...}\endmodality(post)) 
      \replacewith (
             ==> {#loc:=javaDivLong(#se, #seLong)}
                       \modality{#normalassign}{.. ...}\endmodality(post));
      \replacewith (==> #seLong != 0)
      \heuristics(executeIntegerAssignment)
      \displayname "division"
  };

  assignmentDivisionLong2  { 
      \find(==> \modality{#normalassign}{.. 
                #loc=#seLong / #seCharByteShortInt; 
            ...}\endmodality(post)) 
      \replacewith( 
               ==> {#loc:=javaDivLong(#seLong, #seCharByteShortInt)}
                          \modality{#normalassign}{.. ...}\endmodality(post));
      \replacewith (==> #seCharByteShortInt != 0)
      \heuristics(executeIntegerAssignment)
      \displayname "division"
  };

    
  //------------- modulo
  // a modulo operation cannot cause overflow since the magnitude of the result is always
  // less than the magnitude of the divisor (according to Java Lang Spec)

  assignmentModulo {
      \find(==> \modality{#normalassign}{..
                #loc=#se0 % #se1; 
            ...}\endmodality(post)) 
      \replacewith (
            ==> {#loc:=javaMod(#se0, #se1)}
                      \modality{#normalassign}{.. ...}\endmodality(post));
      \replacewith (==> #se1 != 0)
      \heuristics(executeIntegerAssignment)
      \displayname "modulo"
  };

}

 
\rules(programRules:Java,runtimeExceptions:ignore) {

  //------------- division

  assignmentDivisionInt  { 
      \find(\modality{#normalassign}{.. 
                #loc=#seCharByteShortInt0 / #seCharByteShortInt1; 
            ...}\endmodality(post)) 
      \replacewith (
           {#loc:=javaDivInt(#seCharByteShortInt0, #seCharByteShortInt1)}
                            \modality{#normalassign}{.. ...}\endmodality(post))
      \heuristics(executeIntegerAssignment)
      \displayname "division"
  };

  assignmentDivisionLong  { 
      \find(\modality{#normalassign}{.. 
                #loc=#se / #seLong; 
            ...}\endmodality(post)) 
      \replacewith (
             {#loc:=javaDivLong(#se, #seLong)}
                       \modality{#normalassign}{.. ...}\endmodality(post))
      \heuristics(executeIntegerAssignment)
      \displayname "division"
  };

  assignmentDivisionLong2  { 
      \find(\modality{#normalassign}{.. 
                #loc=#seLong / #seCharByteShortInt; 
            ...}\endmodality(post)) 
      \replacewith( 
               {#loc:=javaDivLong(#seLong, #seCharByteShortInt)}
                          \modality{#normalassign}{.. ...}\endmodality(post))
      \heuristics(executeIntegerAssignment)
      \displayname "division"
  };

    
  //------------- modulo
  // a modulo operation cannot cause overflow since the magnitude of the result is always
  // less than the magnitude of the divisor (according to Java Lang Spec)

  assignmentModulo {
      \find(\modality{#normalassign}{..
                #loc=#se0 % #se1; 
            ...}\endmodality(post)) 
     \replacewith (
            {#loc:=javaMod(#se0, #se1)}
                      \modality{#normalassign}{.. ...}\endmodality(post))
        \heuristics(executeIntegerAssignment)
        \displayname "modulo"
  };

}

 
\rules(programRules:Java,runtimeExceptions:allow) {

  //------------- division

  assignmentDivisionInt  { 
      \find(\modality{#normalassign}{.. 
                #loc=#seCharByteShortInt0 / #seCharByteShortInt1; 
            ...}\endmodality(post)) 
      \replacewith (\if (#seCharByteShortInt1!=0)
           \then ({#loc:=javaDivInt(#seCharByteShortInt0, #seCharByteShortInt1)}
                            \modality{#normalassign}{.. ...}\endmodality(post))
           \else (\modality{#normalassign}{.. 
                       throw new java.lang.ArithmeticException(); 
                   ...}\endmodality(post)))
      \heuristics(executeIntegerAssignment)
      \displayname "division"
  };

  assignmentDivisionLong  { 
      \find(\modality{#normalassign}{.. 
                #loc=#se / #seLong; 
            ...}\endmodality(post)) 
      \replacewith (\if (#seLong!=0)
             \then ({#loc:=javaDivLong(#se, #seLong)}
                       \modality{#normalassign}{.. ...}\endmodality(post))
             \else (\modality{#normalassign}{.. 
                        throw new java.lang.ArithmeticException(); 
                    ...}\endmodality(post)))
      \heuristics(executeIntegerAssignment)
      \displayname "division"
  };

  assignmentDivisionLong2  { 
      \find(\modality{#normalassign}{.. 
                #loc=#seLong / #seCharByteShortInt; 
            ...}\endmodality(post)) 
      \replacewith( \if (#seCharByteShortInt!=0)
               \then ({#loc:=javaDivLong(#seLong, #seCharByteShortInt)}
                          \modality{#normalassign}{.. ...}\endmodality(post))
               \else (\modality{#normalassign}{.. 
                         throw new java.lang.ArithmeticException(); 
                         ...}\endmodality(post)))
      \heuristics(executeIntegerAssignment)
      \displayname "division"
  };

    
  //------------- modulo
  // a modulo operation cannot cause overflow since the magnitude of the result is always
  // less than the magnitude of the divisor (according to Java Lang Spec)

  assignmentModulo {
      \find(\modality{#normalassign}{..
                #loc=#se0 % #se1; 
            ...}\endmodality(post)) 
     \replacewith (\if (#se1!=0)
            \then ({#loc:=javaMod(#se0, #se1)}
                      \modality{#normalassign}{.. ...}\endmodality(post))
            \else (\modality{#normalassign}{.. 
                        throw new java.lang.ArithmeticException(); 
                     ...}\endmodality(post)))
        \heuristics(executeIntegerAssignment)
        \displayname "modulo"
  };

}

\rules(programRules:Java) {

  //------------- subtraction

  assignmentSubtractionInt  { 
      \find (\modality{#normalassign}{.. 
                 #loc = #seCharByteShortInt0 - #seCharByteShortInt1; 
             ...}\endmodality(post)) 
      \replacewith ({#loc:=javaSubInt(#seCharByteShortInt0,#seCharByteShortInt1)}
           \modality{#normalassign}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "subtraction"
  };

  assignmentSubtractionLong  { 
     \find (\modality{#normalassign}{.. 
                #loc=#seCharByteShortInt - #seLong; 
            ...}\endmodality(post)) 
     \replacewith({#loc:=javaSubLong(#seCharByteShortInt,#seLong)}
                      \modality{#normalassign}{.. ...}\endmodality(post)) 
     \heuristics(executeIntegerAssignment)
     \displayname "subtraction"
  };

  assignmentSubtractionLong2 { 
     \find (\modality{#normalassign}{.. 
                #loc=#seLong - #seCharByteShortInt; 
            ...}\endmodality(post)) 
     \replacewith(
           {#loc:=javaSubLong(#seLong,#seCharByteShortInt)}
                \modality{#normalassign}{.. ...}\endmodality(post)) 
     \heuristics(executeIntegerAssignment)
     \displayname "subtraction"
  };

  assignmentSubtractionLong3  { 
      \find (\modality{#normalassign}{.. 
                  #loc=#seLong0 - #seLong1; 
             ...}\endmodality(post)) 
      \replacewith ({#loc:=javaSubLong(#seLong0, #seLong1)}
             \modality{#normalassign}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "subtraction"
  };

  //------------- addition

  assignmentAdditionInt  { 
      \find(\modality{#normalassign}{.. 
                #loc=#seCharByteShortInt0 + #seCharByteShortInt1; 
            ...}\endmodality(post)) 
     \replacewith(
         {#loc:=javaAddInt(#seCharByteShortInt0,#seCharByteShortInt1)}
             \modality{#normalassign}{.. ...}\endmodality(post)) 
     \heuristics(executeIntegerAssignment)
     \displayname "addition"
  };

  assignmentAdditionLong  { 
     \find(\modality{#normalassign}{.. 
               #loc=#seCharByteShortInt + #seLong; 
           ...}\endmodality(post)) 
     \replacewith(
           {#loc:=javaAddLong(#seCharByteShortInt,#seLong)}
                \modality{#normalassign}{.. ...}\endmodality(post)) 
     \heuristics(executeIntegerAssignment)
     \displayname "addition"
  };

  assignmentAdditionLong2  { 
     \find(\modality{#normalassign}{.. 
               #loc=#seLong + #seCharByteShortInt; 
           ...}\endmodality(post)) 
     \replacewith({#loc:=javaAddLong(#seLong,#seCharByteShortInt)}
                       \modality{#normalassign}{.. ...}\endmodality(post)) 
     \heuristics(executeIntegerAssignment)
     \displayname "addition"
  };

  //reviewed 04/20/2004, St.S.
  assignmentAdditionLong3 { 
      \find(\modality{#normalassign}{.. 
                #loc=#seLong0 + #seLong1; 
            ...}\endmodality(post)) 
      \replacewith({#loc:=javaAddLong(#seLong0,#seLong1)}
                         \modality{#normalassign}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "addition"
  };

  //------------- binary "and"

  assignmentBitwiseAndInt { 
      \find (\modality{#normalassign}{.. 
                  #loc=#seCharByteShortInt0 & #seCharByteShortInt1; 
             ...}\endmodality(post)) 
       \replacewith(
             {#loc:=javaBitwiseAndInt(#seCharByteShortInt0,#seCharByteShortInt1)}
                           \modality{#normalassign}{.. ...}\endmodality(post)) 
       \heuristics(executeIntegerAssignment)
       \displayname "bitwiseAnd"
  };

  assignmentBitwiseAndLong { 
      \find (\modality{#normalassign}{.. 
                  #loc=#seCharByteShortInt & #seLong; 
             ...}\endmodality(post)) 
      \replacewith(
            {#loc:=javaBitwiseAndLong(#seCharByteShortInt,#seLong)}
                  \modality{#normalassign}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "bitwiseAnd"
  };

  assignmentBitwiseAndLong2 { 
        \find (\modality{#normalassign}{.. 
                    #loc=#seLong & #seCharByteShortInt; 
               ...}\endmodality(post)) 
        \replacewith (
             {#loc:=javaBitwiseAndLong(#seLong,#seCharByteShortInt)}
                   \modality{#normalassign}{.. ...}\endmodality(post)) 
        \heuristics(executeIntegerAssignment)
        \displayname "bitwiseAnd"
  };

  assignmentBitwiseAndLong3 { 
      \find (\modality{#normalassign}{.. 
                  #loc=#seLong0 & #seLong1; 
             ...}\endmodality(post)) 
      \replacewith (
             {#loc:=javaBitwiseAndLong(#seLong0,#seLong1)}
                   \modality{#normalassign}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "bitwiseAnd"
  };

  //------------- binary "or"

  assignmentBitwiseOrInt { 
      \find (\modality{#normalassign}{.. 
                  #loc=#seCharByteShortInt0 | #seCharByteShortInt1; 
             ...}\endmodality(post)) 
      \replacewith (
           {#loc:=javaBitwiseOrInt(#seCharByteShortInt0,#seCharByteShortInt1)}
                 \modality{#normalassign}{.. ...}\endmodality(post)) 
         \heuristics(executeIntegerAssignment)
         \displayname "bitwiseOr"
  };

  assignmentBitwiseOrLong { 
      \find (\modality{#normalassign}{.. 
                  #loc=#seCharByteShortInt | #seLong; 
             ...}\endmodality(post)) 
      \replacewith (
           {#loc:=javaBitwiseOrLong(#seCharByteShortInt,#seLong)}
                 \modality{#normalassign}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "bitwiseOr"
  };

  //reviewed 04/20/2004, St.S.
  assignmentBitwiseOrLong2 { 
      \find (\modality{#normalassign}{.. 
                  #loc=#seLong | #seCharByteShortInt; 
             ...}\endmodality(post)) 
      \replacewith (
           {#loc:=javaBitwiseOrLong(#seLong,#seCharByteShortInt)}
                 \modality{#normalassign}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "bitwiseOr"
  };

  //reviewed 04/20/2004, St.S.
  assignmentBitwiseOrLong3 { 
      \find (\modality{#normalassign}{.. 
                  #loc=#seLong0 | #seLong1; 
             ...}\endmodality(post)) 
      \replacewith (
            {#loc:=javaBitwiseOrLong(#seLong0,#seLong1)}
                 \modality{#normalassign}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "bitwiseOr"
  };

  //------------- binary "xor"

  assignmentBitwiseXOrInt { 
       \find (\modality{#normalassign}{.. 
                  #loc=#seCharByteShortInt0 ^ #seCharByteShortInt1; 
              ...}\endmodality(post)) 
       \replacewith(
            {#loc:=javaBitwiseXOrInt(#seCharByteShortInt0,#seCharByteShortInt1)}
                         \modality{#normalassign}{.. ...}\endmodality(post)) 
       \heuristics(executeIntegerAssignment)
       \displayname "bitwiseXOr"
  };

  assignmentBitwiseXOrLong { 
      \find (\modality{#normalassign}{.. 
                 #loc=#seCharByteShortInt ^ #seLong; 
             ...}\endmodality(post)) 
      \replacewith (
           {#loc:=javaBitwiseXOrLong(#seCharByteShortInt,#seLong)}
                \modality{#normalassign}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "bitwiseXOr"
  };

  assignmentBitwiseXOrLong2 { 
        \find (\modality{#normalassign}{.. 
                   #loc=#seLong ^ #seCharByteShortInt; 
               ...}\endmodality(post)) 
        \replacewith (
             {#loc:=javaBitwiseXOrLong(#seLong,#seCharByteShortInt)}
                  \modality{#normalassign}{.. ...}\endmodality(post)) 
        \heuristics(executeIntegerAssignment)
        \displayname "bitwiseXOr"
  };

  assignmentBitwiseXOrLong3 { 
      \find (\modality{#normalassign}{.. 
                 #loc=#seLong0 ^ #seLong1; 
             ...}\endmodality(post)) 
      \replacewith (
            {#loc:=javaBitwiseXOrLong(#seLong0,#seLong1)}
                 \modality{#normalassign}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "bitwiseXOr"
  };

  //------------- binary shift-right

  assignmentShiftRightInt { 
      \find (\modality{#normalassign}{.. 
                 #loc=#seCharByteShortInt0 >> #se; 
              ...}\endmodality(post)) 
      \replacewith (
           {#loc:=javaShiftRightInt(#seCharByteShortInt0,#se)}
                \modality{#normalassign}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "shift"};

  assignmentShiftRightLong { 
      \find (\modality{#normalassign}{.. 
                 #loc=#seLong0 >> #se; 
             ...}\endmodality(post)) 
      \replacewith (
           {#loc:=javaShiftRightLong(#seLong0,#se)}
                \modality{#normalassign}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "shift"
  };

  //------------- binary shift-left

  assignmentShiftLeftInt { 
      \find (\modality{#normalassign}{.. 
                 #loc=#seCharByteShortInt0 << #se; ...}
             \endmodality(post)) 
      \replacewith(
           {#loc:=javaShiftLeftInt(#seCharByteShortInt0,#se)}
                \modality{#normalassign}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "shift"
  };

  assignmentShiftLeftLong { 
      \find (\modality{#normalassign}{.. 
                 #loc=#seLong0 << #se; 
             ...}\endmodality(post)) 
      \replacewith (
            {#loc:=javaShiftLeftLong(#seLong0,#se)}
                 \modality{#normalassign}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "shift"
  };

  //------------- binary unsigned-shift-right

  assignmentUnsignedShiftRightInt  { 
      \find (\modality{#normalassign}{.. 
                 #loc=#seCharByteShortInt0 >>> #se; ...}
             \endmodality(post)) 
      \replacewith (
            {#loc:=javaUnsignedShiftRightInt(#seCharByteShortInt0,#se)}
                \modality{#normalassign}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "shift"
  };

  assignmentUnsignedShiftRightLong { 
      \find (\modality{#normalassign}{.. 
                 #loc=#seLong0 >>> #se; 
             ...}\endmodality(post)) 
     \replacewith ({#loc:=javaUnsignedShiftRightLong(#seLong0,#se)}
                        \modality{#normalassign}{.. ...}\endmodality(post)) 
     \heuristics(executeIntegerAssignment)
     \displayname "shift"
  };


//------------------------------------------------------------------------
// ----------- Rules for unary plus, minus, and negation
//------------------------------------------------------------------------

  unaryMinusInt { 
     \find (\modality{#normalassign}{.. 
                #loc = - #seCharByteShortInt; 
            ...}\endmodality(post)) 
     \replacewith({#loc:=javaUnaryMinusInt(#seCharByteShortInt)}
                      \modality{#normalassign}{..  ...}\endmodality(post))
     \heuristics (executeIntegerAssignment)
     \displayname "unaryMinus"
  };

  unaryMinusLong { 
      \find (\modality{#normalassign}{.. 
                 #loc = - #seLong; 
             ...}\endmodality(post)) 
      \replacewith( {#loc:=javaUnaryMinusLong(#seLong)}
                        \modality{#normalassign}{..  ...}\endmodality(post))
      \heuristics (executeIntegerAssignment)
      \displayname "unary_minus"
  };

  bitwiseNegation { 
      \find (\modality{#normalassign}{.. #loc = ~ #se; ...}\endmodality(post)) 
      \replacewith({#loc:=javaBitwiseNegation(#se)}
          \modality{#normalassign}{..  ...}\endmodality(post)) 
      \heuristics (executeIntegerAssignment)
      \displayname "invertBits"
  };


//------------------------------------------------------------------------
// ------------- Rules for Java types narrowing casts
//------------------------------------------------------------------------

  narrowingByteCastShort { 
     \find(\modality{#normalassign}{..
               #loc = (byte) #seShort; 
           ...}\endmodality(post))
     \replacewith ({#loc := javaCastByte(#seShort)}
           \modality{#normalassign}{..  ...}\endmodality(post)) 
     \heuristics (executeIntegerAssignment)
     \displayname "cast" 
  };

  narrowingByteCastInt { 
      \find (\modality{#normalassign}{.. 
                 #loc = (byte) #seInt; 
             ...}\endmodality(post))
      \replacewith ({#loc := javaCastByte(#seInt)}
           \modality{#normalassign}{..  ...}\endmodality(post)) 
      \heuristics (executeIntegerAssignment)
      \displayname "cast" 
  };

  narrowingByteCastLong { 
       \find (\modality{#normalassign}{.. 
                  #loc = (byte) #seLong; 
              ...}\endmodality(post))
       \replacewith ({#loc := javaCastByte(#seLong)}
                         \modality{#normalassign}{..  ...}\endmodality(post)) 
       \heuristics (executeIntegerAssignment)
       \displayname "cast" 
  };

  narrowingShortCastInt { 
       \find (\modality{#normalassign}{.. 
                  #loc = (short) #seInt; 
               ...}\endmodality(post))
       \replacewith ({#loc := javaCastShort(#seInt)}
           \modality{#normalassign}{..  ...}\endmodality(post)) 
       \heuristics (executeIntegerAssignment)
       \displayname "cast" 
  };

  narrowingShortCastLong { 
      \find (\modality{#normalassign}{.. 
                 #loc = (short) #seLong; 
             ...}\endmodality(post))
      \replacewith ({#loc := javaCastShort(#seLong)}
            \modality{#normalassign}{..  ...}\endmodality(post)) 
      \heuristics (executeIntegerAssignment)
      \displayname "cast" 
  };

  narrowingIntCastLong { 
      \find (\modality{#normalassign}{.. #loc = (int) #seLong; ...}\endmodality(post))
      \replacewith ({#loc := javaCastInt(#seLong)}
                        \modality{#normalassign}{..  ...}\endmodality(post)) 
      \heuristics (executeIntegerAssignment)
      \displayname "cast" 
  };

  narrowingCharCastByte { 
      \find (\modality{#normalassign}{.. 
                 #loc = (char) #seByte; 
             ...}\endmodality(post))
      \replacewith (
           {#loc := javaCastChar(#seByte)}
               \modality{#normalassign}{..  ...}\endmodality(post)) 
      \heuristics (executeIntegerAssignment)
      \displayname "cast" 
  };

  narrowingCharCastShort { 
      \find (\modality{#normalassign}{.. 
                 #loc = (char) #seShort; 
             ...}\endmodality(post))
      \replacewith (
            {#loc := javaCastChar(#seShort)}
                 \modality{#normalassign}{..  ...}\endmodality(post)) 
      \heuristics (executeIntegerAssignment)
      \displayname "cast" 
  };

  narrowingCharCastInt { 
      \find (\modality{#normalassign}{.. 
                 #loc = (char) #seInt; 
             ...}\endmodality(post))
      \replacewith ({#loc := javaCastChar(#seInt)}
           \modality{#normalassign}{..  ...}\endmodality(post)) 
      \heuristics (executeIntegerAssignment)
      \displayname "cast" 
  };

  narrowingCharCastLong { 
      \find (\modality{#normalassign}{.. 
                 #loc = (char) #seLong; 
             ...}\endmodality(post))
      \replacewith ({#loc := javaCastChar(#seLong)}
             \modality{#normalassign}{..  ...}\endmodality(post)) 
      \heuristics (executeIntegerAssignment)
      \displayname "cast" 
  };
}
