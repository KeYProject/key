// This file is part of KeY - Integrated Deductive Software Design
// Copyright (C) 2001-2009 Universitaet Karlsruhe Germany
//                         Universitaet Koblenz-Landau, Germany
//                         and Chalmers University of Technology, Sweden
//
// The KeY system is protected by the GNU General Public License. 
// See LICENSE.TXT for details.
//
//

\sorts{
 boolean;
}

\schemaVariables{
  \term boolean bo ;
  \term boolean b1, b2 ;
  \term[rigid] boolean br ;
  \formula c;
  \variables boolean x ;
}

\functions{
	boolean TRUE;
	boolean FALSE;
}

\rules {
	boolean_equal { \find (bo = bo) \replacewith(true)
	\heuristics(simplify_boolean, concrete)};

	boolean_equal_2 { \find (b1=TRUE <-> b2=TRUE) \replacewith(b1=b2) 
			       \heuristics(simplify_boolean, concrete, smt_axiom_not_verified)
			       \displayname "boolean_equal"
			};

	boolean_not_equal_1 { \find (TRUE = FALSE) \replacewith(false) 
                              \heuristics(simplify_boolean, concrete)
                              \displayname "boolean_not_equal"
                            };
	boolean_not_equal_2 { \find (FALSE = TRUE) \replacewith(false) 
                              \heuristics(simplify_boolean, concrete)
                              \displayname "boolean_not_equal"
                            };


	true_to_not_false {\find (bo=TRUE) \replacewith(!bo=FALSE) };

 //anything to ...=TRUE:

        false_to_not_true {
		\find (bo = FALSE) 
		\replacewith(!bo=TRUE)
		\heuristics(simplify_boolean, concrete)};       

	boolean_true_commute { 
           \find (TRUE = bo) \replacewith (bo = TRUE) 
           \heuristics(simplify_boolean)
           \displayname "boolean commute"};

	boolean_false_commute { 
           \find (FALSE = bo) \replacewith (bo = FALSE) 
           \heuristics(simplify_boolean)
           \displayname "boolean commute"};


	ex_bool {\find(\exists x; c) 
           \replacewith({\subst x; FALSE}c | {\subst x; TRUE}c)
           \heuristics(boolean_cases) };

	all_bool {\find(\forall x; c) 
           \replacewith({\subst x; FALSE}c & {\subst x; TRUE}c)
           \heuristics(boolean_cases) };


///////////////////////////////////////////////////////////////////////////////

  apply_eq_boolean { \assumes ( ==> bo = TRUE ) \find ( bo ) \sameUpdateLevel
                                          \replacewith ( FALSE )
                     \heuristics ( apply_equations) };

  apply_eq_boolean_2 { \assumes ( ==> bo = FALSE ) \find ( bo ) \sameUpdateLevel
                                           \replacewith ( TRUE )
                     \heuristics ( apply_equations )
                     \displayname "apply_eq_boolean" };

  apply_eq_boolean_rigid { \assumes ( ==> br = TRUE ) \find ( br )
                                                \replacewith ( FALSE )
                           \heuristics ( apply_equations )
                           \displayname "apply_eq_boolean" };

  apply_eq_boolean_rigid_2 { \assumes ( ==> br = FALSE ) \find ( br )
                                                   \replacewith ( TRUE )
                             \heuristics ( apply_equations)
                             \displayname "apply_eq_boolean" };

}
