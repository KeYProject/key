
rem Please edit the next line if you want to set up TG_CLASSPATH variable
rem SET TG_CLASSPATH=
rem SET TG_EXT_CLASSPATH=

if not exist "%TOGETHER_HOME%" (goto MISSING_TOGETHER )
SET TGH=%TOGETHER_HOME%

rem !!!!!!! Please read important information. !!!!!!
rem Note: All environment variables before the first Java command in this batch file
rem will be set in Together.exe and TogetherCon.exe.
SET JDK=%JAVA_HOME%
SET JRE=%JDK%
SET JDK_RT_JAR=%JRE%\lib\rt.jar
SET JDK_TOOLS_JAR=%JDK%\lib\tools.jar

rem Do not use empty JAVA_OPTIONS under Win95/98/Me
rem Do not use values with "=" under Win95/98/Me
SET JAVA_OPTIONS="-noverify -Xms64m -Xmx768m"

SET HOME=%HOMEDRIVE%%HOMEPATH%

rem #############
rem KeY CLASSPATH
rem #############

set key_ext_jars=antlr.jar recoderKey.jar dresden-ocl-demo.jar xerces.jar jargs.jar

set keyclasspath=%KEY_HOME%\system\key.jar;%KEY_HOME%\keypatterns\

if not exist "%KEY_LIB%\antlr.jar" (goto MISSING_LIBRARY ) else (set keyclasspath=%keyclasspath%;%KEY_LIB%\antlr.jar)
if not exist "%KEY_LIB%\recoderKey.jar" (goto MISSING_LIBRARY ) else (set keyclasspath=%keyclasspath%;%KEY_LIB%\recoderKey.jar)
if not exist "%KEY_LIB%\dresden-ocl-demo.jar" (goto MISSING_LIBRARY ) else (set keyclasspath=%keyclasspath%;%KEY_LIB%\dresden-ocl-demo.jar)
if not exist "%KEY_LIB%\xerces.jar" (goto MISSING_LIBRARY ) else (set keyclasspath=%keyclasspath%;%KEY_LIB%\xerces.jar)
if not exist "%KEY_LIB%\jargs.jar" (goto MISSING_LIBRARY ) else (set keyclasspath=%keyclasspath%;%KEY_LIB%\jargs.jar)
if not exist "%KEY_LIB%\log4j.jar" (goto MISSING_LIBRARY ) else (set keyclasspath=%keyclasspath%;%KEY_LIB%\log4j.jar)
set keysysprops=""

rem 
rem  User specific Together configuration
rem 

if not exist "%HOME%\.key" goto CREATE_DEFAULT_CONFIG

:RETURN_TO_CALLER:
rem  Fallback for KeY-specific binaries
set PATH=%PATH%;%KEY_LIB%

if not .%OS%.==.Windows_NT. goto WIN95



:WINNT
SET PATH=%TGH%\bin\win32;%JDK%\bin;%PATH%

rem This line is used only under Windows NT, but this value of JAVA_OPTIONS is used
rem in Together.exe and TogetherCon.exe under all Windows platforms.

"%JDK%\bin\java" -Dkey.home="%KEY_HOME%" -Dkey.pattern.path="%KEY_HOME%\keypatterns" -Dkey.simplify.logdir="%KEY_SIMPLIFY_LOG_DIR%" -Djvm.dir="%JDK%\bin" "%JAVA_OPTIONS%" -cp "%TG_EXT_CLASSPATH%;%TGH%\lib\diagramconverter.jar;%TGH%\lib\together.jar;%TGH%\lib\tgscilib.jar;%TGH%\lib\openapi.jar;%TGH%\modules;%TGH%\lib\gifs.zip;%TGH%\out\classes;%TGH%\lib\i18n;%TGH%\lib\jgl.zip;%TGH%\lib\jacl.jar;%TGH%\lib\coroutine;%TGH%\lib\Java2COM;%TGH%\lib\uddi4j.jar;%TGH%\lib\mail.jar;%TGH%\lib\xalan.jar;%TGH%\lib\xerces.jar;%TGH%\lib\jndi.jar;%TGH%\lib\tcljava.jar;%TGH%\lib\jcvs\jars\jcvsii.jar;%TGH%\lib\jcpagelayout.jar;%TGH%\lib\misclib.zip;%TGH%\lib\javax.jar;%JDK_TOOLS_JAR%;%TGH%\lib\jhall.jar;%TGH%\help\together-help.jar;%TGH%\bundled\tomcat\lib\servlet.jar;%TGH%\lib\soap.jar;%TGH%\bundled\j2ee\lib\j2ee.jar;%TGH%\lib\guiBuilderBeanInfo.jar;%TG_CLASSPATH%;%JDK_RT_JAR%;%TGH%\modules\com\togethersoft\modules\caliberrm\lib\CaliberRM.jar;%TGH%\lib\xsltc.jar;%keyclasspath%" com.togethersoft.together.Main -config.path="%HOME%\.key\configDefaultSnapShot.config" %1 %2 %3 %4 %5 %6 %7
exit /b

:WIN95
SET PATH="%TGH%\bin\win32;%JDK%\bin;%PATH%"
if .%TGH%. == .. goto ERROR_IN_ENVIRONMENT
if .%JDK%. == .. goto ERROR_IN_ENVIRONMENT
if .%JDK_RT_JAR%. == .. goto ERROR_IN_ENVIRONMENT
if .%JDK_TOOLS_JAR%. == .. echo No tools.jar found. No javac or javadoc for you. That's OK.
if .%JAVA_OPTIONS%. == .. goto ERROR_IN_ENVIRONMENT
echo %PATH% | find /C /I "%TGH%\bin\win32;%JDK%\bin;" >nul
if not errorlevel 1 goto AFTER_PATH
:ERROR_IN_ENVIRONMENT
if .%1.==.-recursive. goto PRINT_ERROR
echo Increasing environment space
command.com /E:16384 /C Together.bat -recursive %1 %2 %3 %4 %5 %6 %7
exit /b
:PRINT_ERROR
echo.
echo ERROR WHEN RUNNING Together.bat - "Out of environment space".
echo Increase environment space using batch-file with command:
echo "command.com /E:16384 /C Together.bat"
exit /b
:CREATE_DEFAULT_CONFIG
md "%HOME%\.key"
echo config.level.$internal.group = basic > "%HOME%\.key\configDefaultSnapShot.config"
echo config.level.$internal.0 = $TOGETHER_LIB$\internal.config >> "%HOME%\.key\configDefaultSnapShot.config" 
echo config.level.$default.group = session >> "%HOME%\.key\configDefaultSnapShot.config" 
echo config.level.$default.0 = $TOGETHER_CONFIG$\*.config >> "%HOME%\.key\configDefaultSnapShot.config" 
echo config.level.$default.1 = %KEY_HOME%\config\*.config >> "%HOME%\.key\configDefaultSnapShot.config" 
echo config.level.$default.write = %HOME%\.key\changes.config >> "%HOME%\.key\configDefaultSnapShot.config" 
echo config.level.$commandLine.group = basic >> "%HOME%\.key\configDefaultSnapShot.config" 
echo config.level.$project.group = project >> "%HOME%\.key\configDefaultSnapShot.config" 
echo config.level.$workspace.group = project >> "%HOME%\.key\configDefaultSnapShot.config" 
echo config.level.$workspace.0 = $PROJECT_DIR$\*.config >> "%HOME%\.key\configDefaultSnapShot.config" 
echo config.level.$workspace.write = $PROJECT_DIR$\$PROJECT_NAME$.tws >> "%HOME%\.key\configDefaultSnapShot.config" 
goto RETURN_TO_CALLER
:AFTER_PATH
if .%1.==.-recursive. shift 1
rem This Java command specifies the MaxPermSize parameter directly because under Win95/98/Me
rem it is impossible to assign an environment variable value containing an equal sign (=)
"%JDK%\bin\java" -Djvm.dir="%JDK%\bin" -Dkey.home="%KEY_HOME%" -Dkey.pattern.path="%KEY_HOME%\keypatterns\" -Dkey.simplify.logdir="%KEY_SIMPLIFY_LOG_DIR%" "%JAVA_OPTIONS%" "-XX:MaxPermSize=64m" -cp "%TG_EXT_CLASSPATH%;%TGH%\lib\diagramconverter.jar;%TGH%\modules\com\togethersoft\modules\caliberrm\lib\CaliberRM.jar;%TGH%\lib\xsltc.jar;%TGH%\lib\together.jar;%TGH%\lib\tgscilib.jar;%TGH%\lib\openapi.jar;%TGH%\modules;%TGH%\lib\gifs.zip;%TGH%\out\classes;%TGH%\lib\i18n;%TGH%\lib\jgl.zip;%TGH%\lib\jacl.jar;%TGH%\lib\coroutine;%TGH%\lib\Java2COM;%TGH%\lib\uddi4j.jar;%TGH%\lib\mail.jar;%TGH%\lib\xalan.jar;%TGH%\lib\xerces.jar;%TGH%\lib\jndi.jar;%TGH%\lib\tcljava.jar;%TGH%\lib\jcvs\jars\jcvsii.jar;%TGH%\lib\jcpagelayout.jar;%TGH%\lib\misclib.zip;%TGH%\lib\javax.jar;%JDK_TOOLS_JAR%;%TGH%\lib\jhall.jar;%TGH%\help\together-help.jar;%TGH%\bundled\tomcat\lib\servlet.jar;%TGH%\lib\soap.jar;%TGH%\bundled\j2ee\lib\j2ee.jar;%TGH%\lib\guiBuilderBeanInfo.jar;%TG_CLASSPATH%;%JDK_RT_JAR%; %keyclasspath%" com.togethersoft.together.Main -config.path="%HOME%\.key\configDefaultSnapShot.config" %1 %2 %3 %4 %5 %6 %7

:MISSING_LIBRARY
echo Missing Library. Please copy all libraries to %KEY_LIB%.
echo At least one of the following libraries has not been found: antlr.jar recoderKey.jar ocl-dresden-demo.jar xerces.jar
exit /b

:MISSING_TOGETHER
echo No Together found. Please set the TOGETHER_HOME variable at the top of this script.
exit /b
