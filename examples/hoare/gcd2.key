\functions {
   int _big;
   int _small;
}

\programVariables {
  int big;
  int small;
  int t;
  int result;
}

/* invariant
      small >= 0 & big >= small &
      (big = 0 <-> _big = 0) &
      (\forall int x; 
           (x > 0 -> ((_big % x = 0 & _small % x = 0) <->
               (big % x = 0 & small % x = 0))))
*/

\hoare {

{ _small >= 0 & _big >= _small }
 
[big := _big || small := _small]

\[{     while (!(small == 0)) {
	    t = big % small;
	    big = small;
	    small = t;
	} 	
	result = big;
}\]

{
  (_big != 0 ->  (_big % result = 0 & _small % result = 0 &
      \forall int x; 
           ((x > 0 & _big % x = 0 & _small % x = 0) ->
               result % x = 0)))
  &
  (_big = 0 -> result = 0)
}
 
}
