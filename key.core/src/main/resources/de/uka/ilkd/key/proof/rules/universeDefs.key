\rules(programRules:Java) {
    // ---------------------------------------------------
    //   Introduction Rules
    // ---------------------------------------------------
    variableDeclarationPayload {
        \schemaVar \formula post;
        \schemaVar \program Variable #v0;
        \schemaVar \program Type #t0;
        \schemaVar \modalOperator {diamond, box, diamond_transaction, box_transaction} #allmodal;
        \schemaVar \program ProgramMethod #pm;

        \find(\modality{#allmodal}{.. @universe.qual.Payload #t0 #v0; ...}\endmodality (post))
        \sameUpdateLevel

        \replacewith(\modality{#allmodal}{..  ...}\endmodality (post))
        \addprogvars(#v0)

        \heuristics(simplify_prog, simplify_prog_subset)
        //\displayname "variableDeclaration"
    };

    variableDeclarationPayloadAssign {
        \schemaVar \modalOperator {diamond, box, diamond_transaction, box_transaction} #allmodal;
        \schemaVar \formula post;
        \schemaVar \program Variable #v0;
        \schemaVar \program Type #t;
        \schemaVar \program VariableInitializer #vi;

        \find(\modality{#allmodal}{.. @universe.qual.Payload #t #v0 = #vi; ...}\endmodality (post))
        \replacewith(\modality{#allmodal}{.. @universe.qual.Payload #t #v0; #v0 = #vi; ...}\endmodality (post))

        \heuristics(simplify_prog, simplify_prog_subset)
        \displayname "variableDeclaration"
    };

    variableDeclarationPeer {
        \schemaVar \formula post;
        \schemaVar \program Variable #v0;
        \schemaVar \program Type #t0;
        \schemaVar \modalOperator {diamond, box, diamond_transaction, box_transaction} #allmodal;

        \find(\modality{#allmodal}{.. @universe.qual.Peer #t0 #v0; ...}\endmodality (post))
        \sameUpdateLevel

        \replacewith(\modality{#allmodal}{..  ...}\endmodality (post))
        \addprogvars(#v0)

        \heuristics(simplify_prog, simplify_prog_subset)
        \displayname "variableDeclaration"
    };

    variableDeclarationPeerAssignVariable {
        \schemaVar \modalOperator {diamond, box, diamond_transaction, box_transaction} #allmodal;
        \schemaVar \formula post;
        \schemaVar \program Variable #v0, #v;
        \schemaVar \program Type #t1, #t;
        \schemaVar \program ProgramMethod #pm;
        \schemaVar \program VariableInitializer #vi;

        \find(\modality{#allmodal}{.#pm@#t(#v).. @universe.qual.Peer #t1 #v0 = #vi; ...}\endmodality (post))
        \sameUpdateLevel
        \varcond(\isLocalVariable(#vi))
        \replacewith(\modality{#allmodal}{.. @universe.qual.Peer #t1 #v0; #v0 = #vi; ...}\endmodality (post))
        \add(#vi != null -> owner(#vi) = owner(#v) ==>)

        \heuristics(simplify_prog, simplify_prog_subset)
        \displayname "variableDeclaration"
    };

    variableDeclarationPeerAssign {
        \schemaVar \modalOperator {diamond, box, diamond_transaction, box_transaction} #allmodal;
        \schemaVar \formula post;
        \schemaVar \program Variable #v0;
        \schemaVar \program Type #t;
        \schemaVar \program VariableInitializer #vi;

        \find(\modality{#allmodal}{.. @universe.qual.Peer #t #v0 = #vi; ...}\endmodality (post))
        \varcond(\not \isLocalVariable(#vi))
        \replacewith(\modality{#allmodal}{.. @universe.qual.Peer #t #v0; #v0 = #vi; ...}\endmodality (post))

        \heuristics(simplify_prog, simplify_prog_subset)
        \displayname "variableDeclaration"
    };

    variableDeclarationRep {
        \schemaVar \formula post;
        \schemaVar \program Variable #v0;
        \schemaVar \program Type #t0;
        \schemaVar \modalOperator {diamond, box, diamond_transaction, box_transaction} #allmodal;

        \find(\modality{#allmodal}{.. @universe.qual.Rep #t0 #v0; ...}\endmodality (post))

        \sameUpdateLevel
        \replacewith(\modality{#allmodal}{..  ...}\endmodality (post))
        \addprogvars(#v0)

        \heuristics(simplify_prog, simplify_prog_subset)
        \displayname "variableDeclaration"
    };

    variableDeclarationRepAssignVariable {
        \schemaVar \modalOperator {diamond, box, diamond_transaction, box_transaction} #allmodal;
        \schemaVar \formula post;
        \schemaVar \program Variable #v0, #v;
        \schemaVar \program Type #t1, #t;
        \schemaVar \program ProgramMethod #pm;
        \schemaVar \program VariableInitializer #vi;

        \find(\modality{#allmodal}{.#pm@#t(#v).. @universe.qual.Rep #t1 #v0 = #vi; ...}\endmodality (post))
        \sameUpdateLevel
        \varcond(\isLocalVariable(#vi))
        \replacewith(\modality{#allmodal}{.. @universe.qual.Rep #t1 #v0; #v0 = #vi; ...}\endmodality (post))
        \add(#vi != null -> owner(#vi) = #v ==>)

        \heuristics(simplify_prog, simplify_prog_subset)
        \displayname "variableDeclaration"
    };

    variableDeclarationRepAssign {
        \schemaVar \modalOperator {diamond, box, diamond_transaction, box_transaction} #allmodal;
        \schemaVar \formula post;
        \schemaVar \program Variable #v0, #v;
        \schemaVar \program Type #t;
        \schemaVar \program ProgramMethod #pm;
        \schemaVar \program VariableInitializer #vi;

        \find(\modality{#allmodal}{.. @universe.qual.Rep #t #v0 = #vi; ...}\endmodality (post))
        \varcond(\not \isLocalVariable(#vi))
        \replacewith(\modality{#allmodal}{.. @universe.qual.Rep #t #v0; #v0 = #vi; ...}\endmodality (post))

        \heuristics(simplify_prog, simplify_prog_subset)
        \displayname "variableDeclaration"
    };

    variableDeclarationDom {
        \schemaVar \formula post;
        \schemaVar \program Variable #v0;
        \schemaVar \program Type #t0;
        \schemaVar \modalOperator {diamond, box, diamond_transaction, box_transaction} #allmodal;

        \find(\modality{#allmodal}{.. @universe.qual.Dom #t0 #v0; ...}\endmodality (post))

        \sameUpdateLevel
        \replacewith(\modality{#allmodal}{..  ...}\endmodality (post))
        \addprogvars(#v0)

        \heuristics(simplify_prog, simplify_prog_subset)
        \displayname "variableDeclaration"
    };

    variableDeclarationDomAssignVariable {
        \schemaVar \modalOperator {diamond, box, diamond_transaction, box_transaction} #allmodal;
        \schemaVar \formula post;
        \schemaVar \program Variable #v0, #v;
        \schemaVar \program Type #t1, #t;
        \schemaVar \program ProgramMethod #pm;
        \schemaVar \program VariableInitializer #vi;

        \find(\modality{#allmodal}{.#pm@#t(#v).. @universe.qual.Dom #t1 #v0 = #vi; ...}\endmodality (post))
        \sameUpdateLevel
        \varcond(\isLocalVariable(#vi))
        \replacewith(\modality{#allmodal}{.. @universe.qual.Dom #t1 #v0; #v0 = #vi; ...}\endmodality (post))
        \add(#vi != null -> dominates(#v, #vi) ==>)

        \heuristics(simplify_prog, simplify_prog_subset)
        \displayname "variableDeclaration"
    };

    variableDeclarationDomAssign {
        \schemaVar \modalOperator {diamond, box, diamond_transaction, box_transaction} #allmodal;
        \schemaVar \formula post;
        \schemaVar \program Variable #v0, #v;
        \schemaVar \program Type #t;
        \schemaVar \program ProgramMethod #pm;
        \schemaVar \program VariableInitializer #vi;

        \find(\modality{#allmodal}{.. @universe.qual.Dom #t #v0 = #vi; ...}\endmodality (post))
        \varcond(\not \isLocalVariable(#vi))
        \replacewith(\modality{#allmodal}{.. @universe.qual.Dom #t #v0; #v0 = #vi; ...}\endmodality (post))

        \heuristics(simplify_prog, simplify_prog_subset)
        \displayname "variableDeclaration"
    };

    instanceCreationAssignmentPeer {
        \schemaVar \modalOperator {diamond, box} #normal;
        \schemaVar \formula post;
        \schemaVar \program ProgramMethod #pm;
        \schemaVar \program Type #t;
        \schemaVar \program SimpleInstanceCreation #n;
        \schemaVar \program LeftHandSide #lhs;
        \schemaVar \program Variable #v0, #v, #v1;

        \find(\modality{#normal}{.#pm@#t(#v).. #lhs = #n; ...}\endmodality (post))
        \sameUpdateLevel

        \varcond(\newTypeOf(#v0, #lhs), \newTypeOf(#v1, #lhs), \hasAnnotation(#n, Peer))
        \replacewith(\modality{#normal}{.. #typeof(#v0) #v0 = #create-object(#n);
                    #constructor-call(#v0, #n);
                            #post-work(#v0);
                @universe.qual.Peer #typeof(#v0) #v1 = #v0;
                #lhs = #v1;
                ...}\endmodality (post))
        \heuristics(method_expand)
    };

    instanceCreationAssignmentRep {
        \schemaVar \modalOperator {diamond, box} #normal;
        \schemaVar \formula post;
        \schemaVar \program ProgramMethod #pm;
        \schemaVar \program Type #t;
        \schemaVar \program SimpleInstanceCreation #n;
        \schemaVar \program LeftHandSide #lhs;
        \schemaVar \program Variable #v0, #v, #v1;

        \find(\modality{#normal}{.#pm@#t(#v).. #lhs = #n; ...}\endmodality (post))
        \sameUpdateLevel

        \varcond(\newTypeOf(#v0, #lhs), \newTypeOf(#v1, #lhs), \hasAnnotation(#n, Rep))
        \replacewith(\modality{#normal}{.. #typeof(#v0) #v0 = #create-object(#n);
                    #constructor-call(#v0, #n);
                            #post-work(#v0);
                @universe.qual.Rep #typeof(#v0) #v1 = #v0;
                #lhs = #v1;
                ...}\endmodality (post))
        \heuristics(method_expand)
    };

    instanceCreationAssignmentDom {
        \schemaVar \modalOperator {diamond, box} #normal;
        \schemaVar \formula post;
        \schemaVar \program ProgramMethod #pm;
        \schemaVar \program Type #t;
        \schemaVar \program SimpleInstanceCreation #n;
        \schemaVar \program LeftHandSide #lhs;
        \schemaVar \program Variable #v0, #v, #v1;

        \find(\modality{#normal}{.#pm@#t(#v).. #lhs = #n; ...}\endmodality (post))
        \sameUpdateLevel

        \varcond(\newTypeOf(#v0, #lhs), \newTypeOf(#v1, #lhs), \hasAnnotation(#n, Dom))
        \replacewith(\modality{#normal}{.. #typeof(#v0) #v0 = #create-object(#n);
                    #constructor-call(#v0, #n);
                            #post-work(#v0);
                @universe.qual.Dom #typeof(#v0) #v1 = #v0;
                #lhs = #v1;
                ...}\endmodality (post))
        \heuristics(method_expand)
    };

    peerField {
        \schemaVar \term Object o;
        \schemaVar \term Field f;
        \schemaVar \term Heap h;

        \find(alpha::select(h, o, f))
        \varcond(\hasAnnotation(f, Peer))
        \add(alpha::select(h, o, f) != null -> owner(Object::cast(o)) = owner(Object::cast(alpha::select(h, o, f))) ==>)
        \heuristics(type_hierarchy_def)
    };

    ownsField {
        \schemaVar \term Object o;
        \schemaVar \term Field f;
        \schemaVar \term Heap h;

        \find(alpha::select(h, o, f))
        \varcond(\hasAnnotation(f, Rep))
        \add(alpha::select(h, o, f) != null -> owner(Object::cast(alpha::select(h, o, f))) = o ==>)
        \heuristics(type_hierarchy_def)
    };

    dominatesField {
        \schemaVar \term Object o;
        \schemaVar \term Field f;
        \schemaVar \term Heap h;

        \find(alpha::select(h, o, f))
        \varcond(\hasAnnotation(f, Dom))
        \add(alpha::select(h, o, f) != null -> dominates(o, Object::cast(alpha::select(h, o, f))) ==>)
        \heuristics(type_hierarchy_def)
    };

    // ---------------------------------------------------
    //    Predicate and Function definitions
    // ---------------------------------------------------

    dominatesDepthDef {
        \schemaVar \term Object x, y;
        \schemaVar \variables Object ov;
        \schemaVar \term int n;

        \find(dominatesDepth(x, y, n))
        \varcond(\notFreeIn(ov, x, y, n))
        \replacewith(\if (n <= 0)
            \then(false)
            \else(\if (n = 1) \then(x = owner(y)) \else(\exists ov; (x = owner(ov) & dominatesDepth(ov, y, n - 1)))))
    };

    dominatesDef {
        \schemaVar \term Object x, y;
        \schemaVar \variables int n;

        \find(dominates(x, y))

        \varcond(\notFreeIn(n, x, y))
        \replacewith(\exists n; (dominatesDepth(x, y, n)))
    };

    // would make sense to be used by the automatics, but is somewhat misused and therefore not accessible by automatics
    undomDef {
        \schemaVar \term Object x, y;

        \find(undom(x, y))

        \replacewith(!dominates(x, y) & !dominates(y, x) & !(x = y))
    };

    // would make sense to be used by the automatics, but is somewhat misused and therefore not accessible by automatics
    createdRepfpDef {
        \schemaVar \term Object x;
        \schemaVar \term Heap h;
        \schemaVar \variables Object y;

        \find(createdRepfp(h, x))

        \varcond(\notFreeIn(y, x, h))

        \replacewith(intersect(infiniteUnion{y;}(\if(boolean::select(h, y, java.lang.Object::<created>) = TRUE) \then(y.*) \else(empty)), repfp(x)))
    };

    repfpDef {
        \schemaVar \term Object x;
        \schemaVar \variables Object y;

        \find(repfp(x))
        \varcond(\notFreeIn(y, x))
        \replacewith(union(x.*, infiniteUnion{y;}(\if(dominates(x, y)) \then(y.*) \else(empty))))
    };


    // ---------------------------------------------------
    //    axioms for ownership
    // ---------------------------------------------------

    dominatesSelf {
        \schemaVar \term Object o;

        \find(dominates(o, o))

        \replacewith(false)

        \heuristics(concrete)
    };

    dominatesMaxDepth {
        \schemaVar \term Object x;
        \schemaVar \variables Object y;
        \schemaVar \variables int m, n;

        \find(x)

        \varcond(\notFreeIn(y, x), \notFreeIn(m, x), \notFreeIn(n, x))

        \add(\exists n; (n >= 0 & (\forall y; (dominates(x, y) ->
              (\exists m; (dominatesDepth(x, y, m) & m < n))))) ==>)
    };
}
