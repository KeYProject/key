\rules(programRules:Java) {
    // -------------------------
    //   DominateDepth rules
    // -------------------------

    \lemma
    dominatesDepthTransitive {
        \schemaVar \term Object x, y, z;
        \schemaVar \term int n1, n2;

        \assumes(dominatesDepth(x, y, n1) ==>)
        \find(dominatesDepth(y, z, n2) ==>)
        \add(dominatesDepth(x, z, n1 + n2) ==>)
    };

    \lemma
    dominatesDepth2Dominates {
        \schemaVar \term int n;
        \schemaVar \term Object x, y;

        \find(dominatesDepth(x, y, n) ==>)

        \add(n > 0 & dominates(x, y) ==>)
    };

    \lemma
    dominatesSameDepth {
        \schemaVar \term Object x, y, z;
        \schemaVar \term int n;

        \assumes(dominatesDepth(x, z, n) ==>)
        \find(dominatesDepth(y, z, n) ==>)
        \add(x = y ==>)
    };

    \lemma
    dominatesLargerDepth {
        \schemaVar \term Object x, y, z;
        \schemaVar \term int n, m;

        \assumes(m > n, dominatesDepth(x, z, n) ==>)
        \find(dominatesDepth(y, z, m) ==>)
        \add(dominatesDepth(y, x, m - n) ==>)
    };

    \lemma
    dominatesSingleDepth {
        \schemaVar \term Object x, y, z;
        \schemaVar \term int n;
        \schemaVar \variables int m;

        \find(dominatesDepth(x, y, n) ==>)

        \varcond(\notFreeIn(m, n, x, y))
        \add(\forall m; (dominatesDepth(x, y, m) -> m = n) ==>)
    };

    // -------------------------
    //   Dominate rules
    // -------------------------

    \lemma
    dominatesTransitive {
        \schemaVar \term Object x, y, z;
        \assumes(dominates(x, y) ==>)
        \find(dominates(y, z) ==>)
        \add(dominates(x, z) ==>)
        \heuristics(simplify_enlarging)
    };

    \lemma
    dominatesNotEqual {
        \schemaVar \term Object x, y;

        \assumes(dominates(x, y) ==>)
        \find(x = y)

        \sameUpdateLevel

        \replacewith(false)

        \heuristics(concrete)
    };

    \lemma
    dominatesNotEqualLeft {
        \schemaVar \term Object x, y;

        \assumes(dominates(x, y) ==>)
        \find(y = x)

        \sameUpdateLevel

        \replacewith(false)

        \heuristics(concrete)
        \displayname "dominatesNotEqual"
    };

    \lemma
    closePeerDominate {
        \schemaVar \term Object x, y;

        \assumes(dominates(y, x) ==>)
        \find(dominates(x, y) ==>)
        \closegoal
        \heuristics(closure)
    };

    \lemma
    dominatesAntisymm {
        \schemaVar \term Object x, y;
        \find(dominates(x, y) ==>)
        \add(==> x = y, dominates(y, x))
    };

    \lemma
    dominatesInverse {
        \schemaVar \term Object x, y;

        \assumes(dominates(x, y) ==>)

        \find(dominates(y, x))

        \sameUpdateLevel

        \replacewith(false)
        \heuristics(concrete)
    };

    \lemma
    dominatesNegTransitive {
        \schemaVar \term Object x, y, z;

        \assumes(dominates(x, z) ==> dominates(x, y))
        \find(dominates(z, y))
        \sameUpdateLevel
        \replacewith(false)
        \heuristics(concrete)
    };

    \lemma
    dominatesNegNotEqual {
        \schemaVar \term Object x, y, z;

        \assumes(dominates(x, z) ==> dominates(x, y))
        \find(y = z)
        \sameUpdateLevel
        \replacewith(false)
        \heuristics(concrete)
    };

    // -------------------------
    //   Own rules
    // -------------------------

    \lemma
    owns2Dominates {
        \schemaVar \term Object x, y;

        \find(owner(y) = x ==>)

        \add(dominates(x, y) ==>)
        \heuristics(simplify_enlarging)
    };

    \lemma
    ownsSelf {
        \schemaVar \term Object o;

        \find(owner(o) = o)

        \replacewith(false)

        \heuristics(concrete)
    };

    \lemma
    closePeerOwn {
        \schemaVar \term Object x, y;

        \assumes(owner(x) = y ==>)
        \find(owner(y) = x ==>)
        \closegoal

        \heuristics(closure)
    };

    // -------------------------
    //   repfp rules
    // -------------------------

    \lemma
    repfpElement {
        \schemaVar \term Object x, y;
        \schemaVar \term Field f;

        \find(elementOf(x, f, repfp(y)))
        \replacewith(dominates(y, x) | x = y)
        \heuristics(simplify)
    };

    \lemma
    repfpSubset {
        \schemaVar \term Object x, y;

        \assumes(dominates(x, y) ==>)

        \find(repfp(y))

        \sameUpdateLevel

        \add(subset(repfp(y), repfp(x)) ==>)
        \heuristics(inReachableStateImplication)
    };

    \lemma
    repfpDisjointComplement {
        \schemaVar \term Object x, y;

        \find(intersect(setMinus(allLocs, repfp(x)), repfp(y)) = empty)

        \replacewith(x = y | dominates(x, y))
        \heuristics(simplify)
    };

    // -------------------------
    //   createdRepfp rules
    // -------------------------

    \lemma
    createdRepfpDisjointComplementRepfp {
        \schemaVar \term Object x, y;
        \schemaVar \term Heap h;

        \assumes(boolean::select(h, y, java.lang.Object::<created>) = TRUE ==>)

        \find(intersect(setMinus(allLocs, repfp(x)), createdRepfp(h, y)) = empty)
        \sameUpdateLevel

        \replacewith(x = y | dominates(x, y))
        \heuristics(simplify)
    };

    \lemma
    createdRepfpDisjointComplement {
        \schemaVar \term Object x, y;
        \schemaVar \term Heap h;

        \assumes(boolean::select(h, x, java.lang.Object::<created>) = TRUE, boolean::select(h, y, java.lang.Object::<created>) = TRUE ==>)
        \find(intersect(setMinus(allLocs, createdRepfp(h, x)), createdRepfp(h, y)) = empty)
        \sameUpdateLevel

        \replacewith(x = y | dominates(x, y))
        \heuristics(simplify)
    };

    \lemma
    createdRepfpElement {
        \schemaVar \term Object x, y;
        \schemaVar \term Heap h;
        \schemaVar \term Field f;

        \find(elementOf(x, f, createdRepfp(h, y)))
        \replacewith((dominates(y, x) | x = y) & boolean::select(h, x, java.lang.Object::<created>) = TRUE)
        \heuristics(simplify)
    };

    // -------------------------
    //   Undom rules
    // -------------------------

    \lemma
    undomSymm {
        \schemaVar \term Object commEqLeft, commEqRight;

        \find(undom(commEqLeft, commEqRight))

        \replacewith(undom(commEqLeft, commEqRight))
        \heuristics(order_terms)
    };

    \lemma
    sameLevelUndom {
        \schemaVar \term Object o, x, y;

        \assumes(owner(x) = o, owner(y) = o ==>)
        \find(==> x = y)

        \add(undom(x, y) ==>)
        \heuristics(simplify_enlarging)
    };

    \lemma
    dominatesSameNotUndom {
        \schemaVar \term Object x, y, z;

        \assumes(dominates(x, z) ==>)
        \find(dominates(y, z) ==>)

        \add(==> undom(x, y))
        \heuristics(inReachableStateImplication)
    };

    \lemma
    undomTransitive {
        \schemaVar \term Object x, y, z;

        \assumes(dominates(y, z) ==>)
        \find(undom(x, y) ==>)

        \add(undom(x, z) ==>)

        \heuristics(simplify_enlarging)
    };

    \lemma
    undomNotDominates {
        \schemaVar \term Object x, y;

        \assumes(undom(x, y) ==>)
        \find(dominates(x, y))
        \sameUpdateLevel
        \replacewith(false)
        \heuristics(concrete)
    };

    \lemma
    undomNotDominatesInv {
        \schemaVar \term Object x, y;

        \assumes(undom(x, y) ==>)
        \find(dominates(y, x))
        \sameUpdateLevel
        \replacewith(false)
        \heuristics(concrete)
        \displayname "undomDominates"
    };

    \lemma
    undomNotEqual {
        \schemaVar \term Object x, y;
        \assumes(undom(x, y) ==>)
        \find(x = y)
        \sameUpdateLevel
        \replacewith(false)
        \heuristics(concrete)
    };

    \lemma
    undomDisjointRepfp {
        \schemaVar \term Object x, y;
        \assumes(undom(x, y) ==>)
        \find(intersect(repfp(x), repfp(y)))
        \sameUpdateLevel
        \replacewith(empty)
        \heuristics(concrete)
    };

    \lemma
    undomDisjointCreatedRepfp {
        \schemaVar \term Object x, y;
        \schemaVar \term Heap h1, h2;

        \assumes(undom(x, y) ==>)
        \find(intersect(createdRepfp(h1, x), createdRepfp(h2, y)))
        \sameUpdateLevel
        \replacewith(empty)
        \heuristics(concrete)
    };

    \lemma
    undomDisjointCreatedRepfpInv {
        \schemaVar \term Object x, y;
        \schemaVar \term Heap h1, h2;

        \assumes(undom(y, x) ==>)
        \find(intersect(createdRepfp(h1, x), createdRepfp(h2, y)))
        \sameUpdateLevel
        \replacewith(empty)
        \heuristics(concrete)
        \displayname "undomDisjointCreatedRepfp"
    };
}
