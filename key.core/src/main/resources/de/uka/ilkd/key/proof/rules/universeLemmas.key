\rules(programRules:Java) {
    \lemma
    dominatesNotEqual {
        \schemaVar \term Object x, y;

        \assumes(dominates(x, y) ==>)
        \find(x = y)

        \sameUpdateLevel

        \replacewith(false)

        \heuristics(simplify)
    };

    \lemma
    dominatesNotEqualLeft {
        \schemaVar \term Object x, y;

        \assumes(dominates(x, y) ==>)
        \find(y = x)

        \sameUpdateLevel

        \replacewith(false)

        \heuristics(simplify)
        \displayname "dominatesNotEqual"
    };

    \lemma
    undomRefl {
        \schemaVar \term Object x, y;

        \find(undom(x, y))

        \replacewith(undom(y, x))
    };

    \lemma
    dominatesTransitive {
        \schemaVar \term Object x, y, z;
        \assumes(dominates(x, y) ==>)
        \find(dominates(y, z) ==>)
        \add(dominates(x, z) ==>)
        \heuristics(simplify)
    };

    \lemma
    closePeerDominate {
        \schemaVar \term Object x, y;

        \assumes(dominates(y, x) ==>)
        \find(dominates(x, y) ==>)
        \closegoal
        \heuristics(simplify)
    };

    \lemma
    dominatesAntisymm {
        \schemaVar \term Object x, y;
        \find(dominates(x, y) ==>)
        \add(==> x = y, dominates(y, x))
    };

    \lemma
    owns2Dominates {
        \schemaVar \term Object x, y;

        \find(x = owner(y) ==>)

        \add(dominates(x, y) ==>)
        \heuristics(simplify)
    };

    \lemma
    owns2DominatesLeft {
        \schemaVar \term Object x, y;

        \find(owner(y) = x ==>)

        \add(dominates(x, y) ==>)
        \heuristics(simplify)
        \displayname "owns2Dominates"
    };

    \lemma
    ownsSelf {
        \schemaVar \term Object o;

        \find(o = owner(o))
        
        \replacewith(false)

        \heuristics(simplify)
    };


    \lemma
    closePeerOwn {
        \schemaVar \term Object x, y;

        \assumes(y = owner(x) ==>)
        \find(x = owner(y) ==>)
        \closegoal

        \heuristics(simplify)
    };

    \lemma
    sameLevelUndom {
        \schemaVar \term Object o, x, y;

        \assumes(o = owner(x), o = owner(y) ==>)
        \find(==> x = y)

        \add(undom(x, y) ==>)
        \heuristics(simplify)
    };


    \lemma 
    repfpElement {
        \schemaVar \term Object x, y;
        \schemaVar \term Field f;

        \find(elementOf(x, f, repfp(y)))
        \replacewith(dominates(y, x) | x = y)
        \heuristics(simplify)
    };

    \lemma 
    dominatesInverse {
        \schemaVar \term Object x, y;

        \assumes(dominates(x, y) ==>)

        \find(dominates(y, x))

        \sameUpdateLevel

        \replacewith(false)
        \heuristics(simplify)
    };

    \lemma 
    repfpSubset {
        \schemaVar \term Object x, y;

        \assumes(dominates(x, y) ==>)

        \find(repfp(y))

        \sameUpdateLevel

        \add(subset(repfp(y), repfp(x)) ==>)
        \heuristics(simplify)
    };

    \lemma 
    dominatesNegTransitive {
        \schemaVar \term Object x, y, z;

        \assumes(dominates(x, z) ==> dominates(x, y))
        \find(dominates(z, y))
        \sameUpdateLevel
        \replacewith(false)
        \heuristics(simplify)
    };

    \lemma 
    dominatesNegNotEqual {
        \schemaVar \term Object x, y, z;

        \assumes(dominates(x, z) ==> dominates(x, y))
        \find(y = z)
        \sameUpdateLevel
        \replacewith(false)
        \heuristics(simplify)
    };

    \lemma 
    repfpDisjointComplement {
        \schemaVar \term Object x, y;

        \find(disjoint(setMinus(allLocs, repfp(x)), repfp(y)))

        \replacewith(x = y | dominates(x, y))
        \heuristics(simplify)
    };

    \lemma 
    createdRepfpDisjointComplementRepfp {
        \schemaVar \term Object x, y;
        \schemaVar \term Heap h;

        \assumes(boolean::select(h, y, java.lang.Object::<created>) = TRUE ==>)

        \find(disjoint(setMinus(allLocs, repfp(x)), createdRepfp(h, y)))
        \sameUpdateLevel

        \replacewith(x = y | dominates(x, y))
        \heuristics(simplify)
    };

    \lemma 
    createdRepfpDisjointComplement {
        \schemaVar \term Object x, y;
        \schemaVar \term Heap h;

        \assumes(boolean::select(h, x, java.lang.Object::<created>) = TRUE, boolean::select(h, y, java.lang.Object::<created>) = TRUE ==>)
        \find(disjoint(setMinus(allLocs, createdRepfp(h, x)), createdRepfp(h, y)))
        \sameUpdateLevel

        \replacewith(x = y | dominates(x, y))
        \heuristics(simplify)
    };

    \lemma 
    createdRepfpElement {
        \schemaVar \term Object x, y;
        \schemaVar \term Heap h;
        \schemaVar \term Field f;

        \find(elementOf(x, f, createdRepfp(h, y)))
        \replacewith((dominates(y, x) | x = y) & boolean::select(h, x, java.lang.Object::<created>) = TRUE)
        \heuristics(simplify)
    };

    \lemma 
    dismissSelectOfDominatedObject {
        \schemaVar \term Heap h;
        \schemaVar \term Object o, u;
        \schemaVar \term Field f1, f2;
        \schemaVar \term any x;

        \assumes(dominates(o, u) ==>)
        \find(alpha::select(store(h, o, f1, x), u, f2))
        \sameUpdateLevel

        \replacewith(alpha::select(h, u, f2))
        \heuristics(simplify)
    };

    \lemma 
    dismissSelectOfDominatingObject {
        \schemaVar \term Heap h;
        \schemaVar \term Object o, u;
        \schemaVar \term Field f1, f2;
        \schemaVar \term any x;

        \assumes(dominates(u, o) ==>)
        \find(alpha::select(store(h, o, f1, x), u, f2))
        \sameUpdateLevel

        \replacewith(alpha::select(h, u, f2))
        \heuristics(simplify)
    };

    \lemma 
    dismissSelectOfDominatedObjectEQ {
        \schemaVar \term Heap h, EQ;
        \schemaVar \term Object o, u;
        \schemaVar \term Field f1, f2;
        \schemaVar \term any x;

        \assumes(store(h, o, f1, x) = EQ, dominates(o, u) ==>)
        \find(alpha::select(EQ, u, f2))
        \sameUpdateLevel

        \replacewith(alpha::select(h, u, f2))
        \heuristics(simplify)
    };

    \lemma 
    dismissSelectOfDominatingObjectEQ {
        \schemaVar \term Heap h, EQ;
        \schemaVar \term Object o, u;
        \schemaVar \term Field f1, f2;
        \schemaVar \term any x;

        \assumes(store(h, o, f1, x) = EQ, dominates(u, o) ==>)
        \find(alpha::select(EQ, u, f2))
        \sameUpdateLevel

        \replacewith(alpha::select(h, u, f2))
        \heuristics(simplify)
    };

    \lemma 
    dismissSelectOfDominatedAnonEQ {
        \schemaVar \term Heap h, h2, EQ;
        \schemaVar \term Object o, u;
        \schemaVar \term Field f;
        \schemaVar \term any x;

        \assumes(boolean::select(h, o, java.lang.Object::<created>) = TRUE, anon(h, repfp(u), h2) = EQ, dominates(o, u) ==>)
        \find(alpha::select(EQ, o, f))
        \sameUpdateLevel

        \replacewith(alpha::select(h, o, f))
        \heuristics(simplify)
    };

    \lemma 
    dismissSelectOfDominatedCreatedAnonEQ {
        \schemaVar \term Heap h, h2, h3, EQ;
        \schemaVar \term Object o, u;
        \schemaVar \term Field f;
        \schemaVar \term any x;

        \assumes(boolean::select(h, o, java.lang.Object::<created>) = TRUE, anon(h, createdRepfp(h3, u), h2) = EQ, dominates(o, u) ==>)
        \find(alpha::select(EQ, o, f))
        \sameUpdateLevel

        \replacewith(alpha::select(h, o, f))
        \heuristics(simplify)
    };

    \lemma 
    dismissSelectOfDominatedAnon {
        \schemaVar \term Heap h, h2;
        \schemaVar \term Object o, u;
        \schemaVar \term Field f;
        \schemaVar \term any x;

        \assumes(boolean::select(h, o, java.lang.Object::<created>) = TRUE, dominates(o, u) ==>)
        \find(alpha::select(anon(h, repfp(u), h2), o, f))
        \sameUpdateLevel

        \replacewith(alpha::select(h, o, f))
        \heuristics(simplify)
    };

    \lemma 
    dismissSelectOfDominatedCreatedAnon {
        \schemaVar \term Heap h, h2, h3;
        \schemaVar \term Object o, u;
        \schemaVar \term Field f;
        \schemaVar \term any x;

        \assumes(boolean::select(h, o, java.lang.Object::<created>) = TRUE, dominates(o, u) ==>)
        \find(alpha::select(anon(h, createdRepfp(h3, u), h2), o, f))
        \sameUpdateLevel

        \replacewith(alpha::select(h, o, f))
        \heuristics(simplify)
    };
}
