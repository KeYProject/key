\rules(programRules:Java) {
    \lemma
    variousSeqConcatAssociative {
        \schemaVar \term Seq a, b, c;

        \find(seqConcat(seqConcat(a, b), c))

        \replacewith(seqConcat(a, seqConcat(b, c)))

        \heuristics(simplify)
    };

    \lemma
    variousDisjointComplement {
        \schemaVar \term LocSet x, y;

        \find(disjoint(setMinus(allLocs, x), y))

        \replacewith(subset(y, x))

        \heuristics(simplify)
    };

    \lemma
    variousReferencedObjectIsCreated {
        \schemaVar \term Heap h;
        \schemaVar \term Object o;
        \schemaVar \term Field f;

        \assumes(wellFormed(h) ==> deltaObject::select(h, o, f) = null)
        \find(boolean::select(h,
                deltaObject::select(h, o, f),
                java.lang.Object::<created>) = TRUE)

        \inSequentState

        \replacewith(true)

        \heuristics(concrete)
    };

    \lemma
    variousSelectOfEmptyAnonEQ {
        \schemaVar \term Heap h, h2, EQ;
        \schemaVar \term Object o;
        \schemaVar \term Field f;

        \assumes(anon(h, empty, h2) = EQ ==> o = null)
        \find(alpha::select(EQ, o, f))

        \inSequentState

        \replacewith(\if(boolean::select(h, o, java.lang.Object::<created>) = TRUE)
            \then(alpha::select(h, o, f))
            \else(alpha::select(h2, o, f)))

        \heuristics(simplify_enlarging)
    };

    \lemma
    variousSelectOfEmptyAnon {
        \schemaVar \term Heap h, h2;
        \schemaVar \term Object o;
        \schemaVar \term Field f;

        \assumes(==> o = null)
        \find(alpha::select(anon(h, empty, h2), o, f))

        \inSequentState

        \replacewith(\if(boolean::select(h, o, java.lang.Object::<created>) = TRUE)
            \then(alpha::select(h, o, f))
            \else(alpha::select(h2, o, f)))

        \heuristics(simplify_enlarging)
    };

    \lemma
    variousDisjointUnion {
        \schemaVar \term LocSet x, y, z;

        \find(disjoint(x, union(y, z)))

        \replacewith(disjoint(x, y) & disjoint(x, z))

        \heuristics(simplify_enlarging)
    };

    \lemma
    variousDisjointUnionInverse {
        \schemaVar \term LocSet x, y, z;

        \find(disjoint(union(y, z), x))

        \replacewith(disjoint(x, y) & disjoint(x, z))

        \heuristics(simplify_enlarging)
        \displayname "variousDisjointUnion"
    };

    \lemma
    variousDisjointSingleton {
        \schemaVar \term Object x;
        \schemaVar \term Field f;
        \schemaVar \term LocSet s;

        \find(disjoint(s, singleton(x, f)))
        \replacewith(!elementOf(x, f, s))
        \heuristics(simplify)
    };

    \lemma
    variousDisjointSingletonInverse {
        \schemaVar \term Object x;
        \schemaVar \term Field f;
        \schemaVar \term LocSet s;

        \find(disjoint(singleton(x, f), s))
        \replacewith(!elementOf(x, f, s))
        \heuristics(simplify)

        \displayname "variousDisjointSingleton"
    };
}
