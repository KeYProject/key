plugins {
    id 'application'
    // Used to create a single executable jar file with all dependencies
    // see task "shadowJar" below
    // https://imperceptiblethoughts.com/shadow/
    id 'com.github.johnrengelman.shadow' version "8.1.1"
}

dependencies {
    //logging implementation used by the slf4j
    implementation 'ch.qos.logback:logback-classic:1.5.6'
    implementation 'info.picocli:picocli:4.7.6'
    implementation(project(":key.core"))
}

shadowJar {
    archiveClassifier.set("exe")
    archiveBaseName.set("key-format")
    mergeServiceFiles()
}

application {
    mainClassName = "de.uka.ilkd.key.nparser.format.KeyFormatFacade"
}


tasks.register('formatKey', JavaExec) {
    mainClass = mainClassName
    workingDir(rootDir)
    classpath = sourceSets.main.runtimeClasspath
    args("format", "key.core/src/main/resources/de/uka/ilkd/key/proof/rules")
}

tasks.register('formatKeyCheck', JavaExec) {
    mainClass = mainClassName
    workingDir(rootDir)
    classpath = sourceSets.main.runtimeClasspath
    args("check", "key.core/src/main/resources/de/uka/ilkd/key/proof/rules")
}