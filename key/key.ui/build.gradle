plugins {
    id 'application'

    // Used to create a single executable jar file with all dependencies
    // see task "shadowJar" below
    // https://imperceptiblethoughts.com/shadow/
    id 'com.github.johnrengelman.shadow' version "7.1.1"
}

dependencies {
    implementation project(":key.core")
    implementation project(":key.core:rifl")

    implementation project(":key.core.proof_references")
    implementation project(":key.core.symbolic_execution")
    implementation project(":key.removegenerics")

    api 'com.miglayout:miglayout-swing:11.0'

    //logging implementation used by the slf4j
    implementation 'ch.qos.logback:logback-classic:1.2.10'

    api 'org.dockingframes:docking-frames-common:1.1.3p1'
    api 'org.dockingframes:docking-frames-core:1.1.3p1'

    runtimeOnly project(":keyext.ui.testgen")

    runtimeOnly project(":keyext.interactionlog")
    runtimeOnly project(":keyext.exploration")
}

task createExamplesZip(type: Zip) {
    description 'Create "examples.zip" containing all KeY examples'
    destinationDirectory = file("$buildDir/resources/main/")
    archiveFileName = "examples.zip"
    from 'examples'
    include '*'
    include '**/**'
}

processResources.dependsOn << createExamplesZip

shadowJar {
    archiveClassifier = "exe"
    archiveBaseName = "key"
}

distZip {
    archiveBaseName = "key"
}

application {
    mainClassName = "de.uka.ilkd.key.core.Main"
}

run {
    systemProperties["key.examples.dir"] = "$projectDir/examples"
    systemProperties["key.contractOrder"] = "$projectDir/contract-order.txt"
    //systemProperties["KeyDebugFlag"] = "on"
    //args "--experimental"

    // this can be used to solve a problem where the OS hangs during debugging of popup menus
    // (see https://docs.oracle.com/javase/10/troubleshoot/awt.htm#JSTGD425)
    //jvmArgs += "-Dsun.awt.disablegrab=true"
}

task runWithProfiler(type: JavaExec) {
    group("application")
    description("""Run key.ui with flight recoder enabled. 
                   See https://www.baeldung.com/java-flight-recorder-monitoring for a how to.""")

    classpath = sourceSets.main.runtimeClasspath
    mainClass = "de.uka.ilkd.key.core.Main"
    jvmArgs = ["-XX:+FlightRecorder",
               "-XX:StartFlightRecording=duration=30s,filename=key_profile.jfr",
               "-XX:FlightRecorderOptions=stackdepth=256"]
    afterEvaluate {
        println "A file key_profile.jfr should be created in folder 'key.ui'. You can open it in IntelliJ."
    }
}
