plugins {
    id 'application'
    
    // Used to create a single executable jar file with all dependencies
    // see task "shadowJar" below
    // https://imperceptiblethoughts.com/shadow/
    id 'com.github.johnrengelman.shadow' version "5.0.0"
}

dependencies {
    implementation project(":key.core")
    implementation project(":key.core:rifl")

    implementation project(":key.core.proof_references")
    implementation project(":key.core.symbolic_execution")
    implementation project(":key.removegenerics")

    implementation 'com.miglayout:miglayout-swing:5.2'

    implementation group: group, name: 'docking-frames-common', version: '1.0'
    implementation group: group, name: 'docking-frames-core', version: '1.0'

    implementation "org.antlr:antlr4-runtime:4.7.1"

    runtimeOnly project(":keyext.ui.testgen")

    runtimeOnly project(":keyext.interactionlog")
    //runtimeOnly project(":keyext.script.ui")
}

task createExamplesZip(type: Zip) {
    description 'Create "examples.zip" containing all KeY examples'
    destinationDirectory = file("$buildDir/resources/main/")
    archiveFileName = "examples.zip"
    from 'examples'
    include '*'
    include '**/**'
}

processResources.dependsOn << createExamplesZip

shadowJar {
    archiveClassifier = "exe"
    archiveBaseName = "key"
}

distZip {
    archiveBaseName = "key"
}

application {
    mainClassName = "de.uka.ilkd.key.core.Main"
}

run {
    systemProperties["key.examples.dir"] = "$projectDir/examples"
    //systemProperties["KeyDebugFlag"] = "on"
    //args << "--experimental"
}

